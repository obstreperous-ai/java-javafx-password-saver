plugins {
    id "application"
    id "org.openjfx.javafxplugin" version "0.0.14"
    id "checkstyle"
    id "pmd"
    id "com.github.spotbugs" version "6.0.27"
    id "org.owasp.dependencycheck" version "12.2.0"
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = "com.example.passwordsaver.App"
}

javafx {
    version = "21.0.2"
    modules = ["javafx.controls", "javafx.fxml", "javafx.graphics"]
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.2"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2"
    implementation "org.bouncycastle:bcprov-jdk18on:1.78.1"
    
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = "10.17.0"
    configFile = file("config/checkstyle/checkstyle.xml")
}

pmd {
    toolVersion = "7.4.0"
    consoleOutput = true
    ruleSets = []
    ruleSetFiles = files("config/pmd/pmd.xml")
}

spotbugs {
    toolVersion = "4.8.6"
}

def nvdKey = System.getenv("NVD_API_KEY") ?: findProperty("nvdApiKey")

dependencyCheck {
    failBuildOnCVSS = 10.0
    analyzers.assemblyEnabled = false
    skipConfigurations = ["testImplementation", "testRuntimeOnly"]
    nvd {
        apiKey = nvdKey
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}
